/*----------------------------------------------------------------------------*/
/* r5f_linker.cmd                                                             */
/*                                                                            */
/* (c) Texas Instruments 2024, All rights reserved.                           */
/*----------------------------------------------------------------------------*/

/* This is the stack that is used by code running within main()
 * In case of NORTOS,
 * - This means all the code outside of ISR uses this stack
 * In case of FreeRTOS
 * - This means all the code until vTaskStartScheduler() is called in main()
 *   uses this stack.
 * - After vTaskStartScheduler() each task created in FreeRTOS has its own stack
 */
--stack_size=4096
/* This is the heap size for malloc() API in NORTOS and FreeRTOS
 * This is also the heap used by pvPortMalloc in FreeRTOS
 */
--heap_size=4096

--retain="*(.irqStack)"
--retain="*(.fiqStack)"
--retain="*(.abortStack)"
--retain="*(.undStack)"
--retain="*(.svcStack)"

-e_vectors  /* This is the entry of the application, _vector MUST be plabed starting address 0x0 */

/* Stack Sizes for various R5F modes */
__IRQ_STACK_SIZE = 256;
__FIQ_STACK_SIZE = 256;
__ABORT_STACK_SIZE = 256;
__UNDEFINED_STACK_SIZE = 256;
__SVC_STACK_SIZE = 4096;

/*----------------------------------------------------------------------------*/
/* Memory Map                                                                 */
MEMORY{
PAGE 0:
    /* 16 Bytes reserved at the end of each section for CRC */
    RESET_VECTORS  (X)  : origin=0x00000000 length= (0x00000100 - 0x10)                 /* Exception vectors */
    TCMA_RAM       (RX) : origin=0x00000100 length= (0x00080000 - 0x100 - 0x10)         /* TCMA RAM 512 KB in eclipsed mode */
    TCMB_RBL_Reserv(RW) : origin=0x08000000 length= (0x00009000 - 0x10)                 /* TCMB RAM 36 KB used by RBL. Do not use for Code and Data Sections */
    TCMB_RAM       (RX) : origin=0x08009000 length= (0x00040000 - 0x9000 - 0x10)        /* TCMB RAM 220 KB */
    IPC_SH_MEM     (RW) : origin=0x88000000 length= (0x00000400 - 0x10)                 /* 1 KB Memory reserved for IPC */
    /* It is not recommended to use this section to store data, code, or RO segments. */
    DSS_L3         (RW) : origin=0x88000400 length= (0x00160000 - 0x0400 - 0x10)        /* DSS L3 including shared RAMs is 1407 KB */   
}

/*----------------------------------------------------------------------------*/
/* Section Configuration                                                      */
SECTIONS{
    /* This has the R5F entry point and vector table, this MUST be at 0x0 */
    .vectors:{} palign(8) > RESET_VECTORS

    /* This has the R5F boot code */
    GROUP {
        .text.hwi: palign(8)
        .text.cache: palign(8)
        .text.mpu: palign(8)
        .text.boot: palign(8)
        .text:abort: palign(8) /* this helps in loading symbols when using XIP mode */
    } > TCMA_RAM

    GROUP {
        .text:   {} align(8)   /* This is where code resides */
        .rodata: {} align(8)   /* This is where const's go */
    } >> TCMA_RAM | TCMB_RAM

    GROUP {
        .data:   {} align(8)   /* This is where initialized globals and static go */
    } >> TCMA_RAM | TCMB_RAM

    GROUP {
        .bss:    {} align(8)   /* This is where uninitialized globals go */
        RUN_START(__BSS_START)
        RUN_END(__BSS_END)
    } >> TCMA_RAM | TCMB_RAM

    /* This is where the stacks for different R5F modes go */
    GROUP {
        .irqstack: {. = . + __IRQ_STACK_SIZE;} align(8)
        RUN_START(__IRQ_STACK_START)
        RUN_END(__IRQ_STACK_END)
        .fiqstack: {. = . + __FIQ_STACK_SIZE;} align(8)
        RUN_START(__FIQ_STACK_START)
        RUN_END(__FIQ_STACK_END)
        .svcstack: {. = . + __SVC_STACK_SIZE;} align(8)
        RUN_START(__SVC_STACK_START)
        RUN_END(__SVC_STACK_END)
        .abortstack: {. = . + __ABORT_STACK_SIZE;} align(8)
        RUN_START(__ABORT_STACK_START)
        RUN_END(__ABORT_STACK_END)
        .undefinedstack: {. = . + __UNDEFINED_STACK_SIZE;} align(8)
        RUN_START(__UNDEFINED_STACK_START)
        RUN_END(__UNDEFINED_STACK_END)
        .sysmem: {} align(8)   /* This is where the malloc heap goes */
        .stack:  {} align(8)   /* This is where the main() stack goes */
    } > TCMA_RAM


    /* It is not recommended to use this section to store data, code, or RO segments. */
    .bss.l3 {} > DSS_L3

    /* Data needed to be put in IPC Shared Memory can be assigned this section name */
    .ipc_sh_mem {} > IPC_SH_MEM
}
/*----------------------------------------------------------------------------*/